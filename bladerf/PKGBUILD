# Maintainer: Dominik Heidler <dheidler@gmail.com>
pkgname=bladerf
pkgver=0.13.0
pkgrel=1
pkgdesc="Driver for bladeRF, allowing general purpose software defined radio (SDR)."
arch=('i686' 'x86_64')
url="https://github.com/Nuand/bladeRF"
license=('GPLv2')
depends=('libusbx>=1.0.12')
makedepends=('git' 'cmake' 'doxygen')

#backup=("etc/bladerf.cfg")

_git_commit='02d0cb38be163523158ab6b11177aa79716c6bef'
_firmware_file='bladeRF_fw_v1.6.1.img'

source=(
	"git://github.com/Nuand/bladeRF#commit=${_git_commit}"
	"http://www.nuand.com/fx3/${_firmware_file}"
	"http://nuand.com/fpga/ddc81d0fb1b653227b5824d8d5fcb444556da175/hostedx40.rbf"
	"http://nuand.com/fpga/ddc81d0fb1b653227b5824d8d5fcb444556da175/hostedx115.rbf"
	"88-bladerf.rules")

md5sums=('SKIP'
         '3cff9d6a2342fb70be9782e5413d4d8c'
         '2be690da46eea7053e6c3488fd7f55a1'
         '61025399248f6a93b0ca38edd631bf87'
         '062aefbf49fc9f9ec3e20b2994eeb8c2')

_extract_ver() {
	cat | head -n1 | sed -e "s/[^ ]* *\([0-9]*\))/\1/"
}

pkgver() {
	_MAJOR=`grep VERSION_INFO_MAJOR "$srcdir/bladeRF/host/libraries/libbladeRF/CMakeLists.txt" | _extract_ver`
	_MINOR=`grep VERSION_INFO_MINOR "$srcdir/bladeRF/host/libraries/libbladeRF/CMakeLists.txt" | _extract_ver`
	_PATCH=`grep VERSION_INFO_PATCH "$srcdir/bladeRF/host/libraries/libbladeRF/CMakeLists.txt" | _extract_ver`
	printf "%s.%s.%s" $_MAJOR $_MINOR $_PATCH
}

build() {
	cd "$srcdir/bladeRF/host"
	mkdir -p build
	cd build
	cmake \
		-DCMAKE_BUILD_TYPE=Release \
		-DINSTALL_UDEV_RULES=OFF \
		-DBUILD_DOCUMENTATION=ON \
		-DCMAKE_INSTALL_PREFIX=/usr ../
	make
}

package() {
	cd "$srcdir/bladeRF/host/build"
	make DESTDIR=${pkgdir} install
	cd "$srcdir"
	install -vD -m644 88-bladerf.rules "${pkgdir}/usr/lib/udev/rules.d/88-bladerf.rules"
	install -vD -m644 $_firmware_file "${pkgdir}/usr/share/bladerf/firmware/${_firmware_file}"
	install -vD -m644 hostedx40.rbf "${pkgdir}/usr/share/bladerf/fpga/hostedx40.rbf"
	install -vD -m644 hostedx115.rbf "${pkgdir}/usr/share/bladerf/fpga/hostedx115.rbf"
}
