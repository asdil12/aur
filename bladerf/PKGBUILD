# Maintainer: Dominik Heidler <dheidler@gmail.com>
pkgname=bladerf
pkgver=0.3.0
pkgrel=1
pkgdesc="Driver for bladeRF, allowing general purpose software defined radio (SDR)."
arch=('i686' 'x86_64')
url="https://github.com/Nuand/bladeRF"
license=('GPLv2')
depends=('libusbx>=1.0.12')
makedepends=('git' 'cmake')

backup=("etc/bladerf.cfg")
source=(
	'git://github.com/Nuand/bladeRF#commit=262d4cd39c70919df2b625f634f4233e00d54361'
	'http://nuand.com/fx3/1.2.img'
	'http://nuand.com/fpga/ddc81d0fb1b653227b5824d8d5fcb444556da175/hostedx40.rbf'
	'http://nuand.com/fpga/ddc81d0fb1b653227b5824d8d5fcb444556da175/hostedx115.rbf'
)
sha1sums=(
	'SKIP'
	'98c45e6f777f78c02f6705a265f24d36da0335b9'
	'10251c8d12bf32bd4d2ec987f904fa3e644883cd'
	'211d3d997b56624d46abfa2eeaeefde345a38c87'
)

build() {
	cd "$srcdir/bladeRF/host"
	mkdir -p build
	cd build
	cmake \
		-DCMAKE_BUILD_TYPE=Release \
		-DINSTALL_UDEV_RULES=OFF \
		-DCMAKE_INSTALL_PREFIX=/usr ../
	make
}

package() {
	cd "$srcdir/bladeRF/host/build"
	make DESTDIR=${pkgdir} install
	cd "$srcdir/bladeRF/host/misc/udev"
	install -vD -m644 88-nuand.rules $pkgdir/usr/lib/udev/rules.d/88-nuand.rules
	cd "$srcdir"
	install -vD -m644 1.2.img $pkgdir/usr/share/bladerf/firmware/1.2.img
	install -vD -m644 hostedx40.rbf $pkgdir/usr/share/bladerf/fpga/hostedx40.rbf
	install -vD -m644 hostedx115.rbf $pkgdir/usr/share/bladerf/fpga/hostedx115.rbf
}
