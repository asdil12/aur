# Maintainer: Dominik Heidler <dheidler@gmail.com>
pkgname=gr-osmosdr
pkgver=0.1.1
pkgrel=3
pkgdesc="GNU Radio source block for Funcube Dongle, RTL-SDR, USRP and OsmoSDR devices"
arch=('i686' 'x86_64')
url="http://sdr.osmocom.org/trac/"
license=('GPL')
depends=('gnuradio' 'swig' 'rtl-sdr' 'libuhd' 'gr-fcdproplus' 'hackrf' 'bladerf')
makedepends=('git' 'cmake' 'boost')
conflicts=('gr-osmosdr-git')

#source=("git://git.osmocom.org/gr-osmosdr#tag=v$pkgver")
source=("git://git.osmocom.org/gr-osmosdr#commit=68ed1854e7d26ca97d1f1f67cee398ae9b095e42")
md5sums=('SKIP')

build() {
	cd "$srcdir/$pkgname"
	mkdir build
	cd build
	cmake \
		-DCMAKE_BUILD_TYPE=Release \
		-DPYTHON_EXECUTABLE=$(which python2) \
		-DPYTHON_INCLUDE_DIR=$(echo /usr/include/python2*) \
		-DPYTHON_LIBRARY=$(echo /usr/lib/libpython2.*.so) \
		-DCMAKE_INSTALL_PREFIX=/usr ../
	make
}

package() {
	cd "$srcdir/$pkgname/build/"
	make DESTDIR=${pkgdir} install
}
